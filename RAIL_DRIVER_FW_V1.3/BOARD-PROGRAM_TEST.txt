//================================================
// Gestion des numeros de piste
// Entre 0 et 3, numéro de piste unique
// Entre 17 et 31 :
// 17 = 0
// 18 = 1
// 19 = 1 0
// 20 = 2
// 21 = 2 0
// 22 = 2 1
// 23 = 2 1 0
// 24 = 3
// 25 = 3 0
// 26 = 3 1
// 27 = 3 1 0
// 28 = 3 2 
// 29 = 3 2 0
// 30 = 3 2 1
// 31 = 3 2 1 0
//================================================

//================================================
// GESTION DES AUTOMATISMES DE LA CARTE MASTER 31
//================================================

// Connect Rail 0	Voie A0
// Connect Rail 1	Voie B1
// Connect Rail 2	Voie C2
// Connect Rail 3	Voie D3

// LPO 0		Feu vert B1
// LPO 1		Feu rouge B1
// LPO 2		Led bouton bleu
// LPO 3		Led mode manuel
// LPO 4		décrochage wagon circuit bas
// LPO 5		non connecté

// GPIO input 0		Bouton aiguillage CROSS rouge
// GPIO input 1		Bouton bleu démarrer
// GPIO input 2		Bouton aiguillage CROSS noir
// GPIO input 3		Contact mode manuel

// Track Status	0	Afficheur 7 segments DIO
// Track Status	1	Afficheur 7 segments CLK
// Track Status	2	Potentiomètre inertie	
// Track Status	3	Potentiomètre vitesse

//================================================
// GESTION DES AUTOMATISMES DE LA CARTE 0
//================================================

// Connect Rail 0	Voie E0
// Connect Rail 1	Voie F1
// Connect Rail 2	Voie G2
// Connect Rail 3	Voie H3

// LPO 0		Feu vert E0
// LPO 1		Feu rouge E0
// LPO 2		Led bouton blanc
// LPO 3		Led mode manuel
// LPO 4		décrochage gare crémaillère haut
// LPO 5		voie CROSS sur voie F (0) / voie CROSS non connecté

// GPIO input 0		Bouton aiguillage D3 / H3 et C2/ G2 rouge
// GPIO input 1		Bouton blanc démarrer
// GPIO input 2		Bouton aiguillage D3 / H3 et C2/ G2 noir
// GPIO input 3		Contact mode manuel

// Track Status	0	Afficheur 7 segments DIO
// Track Status	1	Afficheur 7 segments CLK
// Track Status	2	Potentiomètre inertie	
// Track Status	3	Potentiomètre vitesse

//================================================
// GESTION DES AUTOMATISMES DE LA CARTE 1
//================================================

// Connect Rail 0	Voie I0
// Connect Rail 1	Voie J1
// Connect Rail 2	Voie K2
// Connect Rail 3	Voie L3
// LPO 0		aiguillage E0 / (E0) H1 et E0 / G2
// LPO 1		non connecté (relié à un relai)
// LPO 2		Led bouton jaune
// LPO 3		Led mode manuel
// LPO 4		aiguillage I0 / J1 , K2
// LPO 5		non connecté (relié à un relai)

// GPIO input 0		aiguillage E0 / (E0) H1 et E0 / G2
// GPIO input 1		Bouton Jaune
// GPIO input 2		aiguillage E0 / (E0) H1 et E0 / G2
// GPIO input 3		Contact mode manuel

// Track Status	0	Afficheur 7 segments DIO
// Track Status	1	Afficheur 7 segments CLK
// Track Status	2	Potentiomètre inertie	
// Track Status	3	Potentiomètre vitesse


//================================================
// GESTION DES AUTOMATISMES DE LA CARTE 2
//================================================

// Connect Rail 0	Voie M0
// Connect Rail 1	Voie voiture 1
// Connect Rail 2	Voie voiture 2
// Connect Rail 3	Voie non connectée

// LPO 0		Ouverture barrière passage à niveau 
// LPO 1		Fermeture barrière passage à niveau
// LPO 2		Led bouton orange
// LPO 3		Led mode manuel
// LPO 4		Led tramway devant grue
// LPO 5		Led tramway devant gare terminale 

// GPIO input 0		non connecté
// GPIO input 1		Bouton orange
// GPIO input 2		Détecton passage tramway devant capteur magnétique
// GPIO input 3		Contact mode manuel

// Track Status	0	Afficheur 7 segments DIO
// Track Status	1	Afficheur 7 segments CLK
// Track Status	2	Potentiomètre inertie	
// Track Status	3	Potentiomètre vitesse

//================================================
// GESTION DES AUTOMATISMES DE LA CARTE 3
//================================================

// Connect Rail 0	Voie non connectée
// Connect Rail 1	Voie non connectée
// Connect Rail 2	Voie non connectée
// Connect Rail 3	Voie non connectée

// LPO 0		aiguillage CROSS noir
// LPO 1		aiguillage CROSS rouge
// LPO 2		aiguillage B1 / CROSS (noir cmd manuel double appui)
// LPO 3		aiguillage B1 / CROSS (rouge cmd manuel double appui)
// LPO 4		non connecté (relié à un relai)
// LPO 5		aiguillage D3 / H3 et C2 / G2

// GPIO input 0		aiguillage I0 / J1 , K2
// GPIO input 1		aiguillage I0 / J1 , K2
// GPIO input 2		Commande décrochage wagon gare crémaillère haut
// GPIO input 3		Commande décrochage wagon circuit bas

// Track Status	0	non connecté
// Track Status	1	non connecté
// Track Status	2	non connecté	
// Track Status	3	non connecté


//============================================================
// PRINCIPES GENERAUX DE FONCTIONNEMENT POUR TOUTES LES CARTES
//============================================================

// Le passage en mode manuel
//	- passe en vitesse nulle toutes les voies de la carte
//	- allume la led rouge du mode manuel
//	- Eteint la led du bouton demarrer
//	- Réinitialise tous les timers
//	- Passage en mode manuel

// Le passage en mode automatique
//	 - Eteint la led rouge du bouton mode manuel
//	 - Passage en mode automatique
//	 - L'appui sur le bouton démarrer
//		- Allume la led du bouton démarrer
//		- Lance la séquence automatique programmée 

//============================================================
// REGLES DE NOMAGE DES COMMANDES
// ------------------------------
// M<i> => MANUEL
// A<i> => AUTOMATIQUE
// T<i> => Gestion commandes du TCO
// D<i> => Commande pouvant être désactivée

//============================================================
// GESTION DES MODES MANUELS
//============================================================

////////////////////////////////////////////////////////
// CARTE MASTER 31 pour les 4 voies externes A0,B1,C2,D3
////////////////////////////////////////////////////////

// Passe en vitesse nulle sur toutes les voies de la carte
PROG 31 AUT M1 AUTON BOARD 31 GPIO 3 0 ACT TRACK 31 SPEED 0 FORW INERTIA 0

// Allume la led rouge du mode manuel
PROG 31 AUT M2 AUTON BOARD 31 GPIO 3 0 ACT LPO 3 1

// Eteint la led du bouton demarrer
PROG 31 AUT M3 AUTON BOARD 31 GPIO 3 0 ACT LPO 2 0

// Réinitialise GPIO
PROG 31 AUT M4 AUTON BOARD 31 GPIO 3 0 ACT GPIO 3 0

// passage en mode manuel
PROG 31 AUT M5 AUTON BOARD 31 GPIO 3 0 ACT MANUAL

// Réinitialise GPIO
PROG 31 AUT M6 AUTON BOARD 31 GPIO 1 0 ACT GPIO 1 0

////////////////////////////////////////////////
// CARTE 0 pour les 4 voies internes E0,F1,G2,H3 
////////////////////////////////////////////////

// Passe en vitesse sur nulle toutes les voies de la carte
PROG 0 AUT M1 AUTON BOARD 0 GPIO 3 0 ACT TRACK 31 SPEED 0 FORW INERTIA 0

// Allume la led rouge du mode manuel
PROG 0 AUT M2 AUTON BOARD 0 GPIO 3 0 ACT LPO 3 1

// Eteint la led du bouton demarrer
PROG 0 AUT M3 AUTON BOARD 0 GPIO 3 0 ACT LPO 2 0

// Réinitialise GPIO
PROG 0 AUT M4 AUTON BOARD 0 GPIO 3 0 ACT GPIO 3 0

// passage en mode manuel
PROG 0 AUT M5 AUTON BOARD 0 GPIO 3 0 ACT MANUAL

// Réinitialise GPIO
PROG 0 AUT M6 AUTON BOARD 0 GPIO 1 0 ACT GPIO 1 0

/////////////////////////////////////////////////////////
// CARTE 1 pour les 4 voies de la crémaillère I0,J1,K2,L3
/////////////////////////////////////////////////////////

// Passe en vitesse sur nulle toutes les voies de la carte
PROG 1 AUT M1 AUTON BOARD 1 GPIO 3 0 ACT TRACK 31 SPEED 0 FORW INERTIA 0

// Allume la led rouge du mode manuel
PROG 1 AUT M2 AUTON BOARD 1 GPIO 3 0 ACT LPO 3 1

// Eteint la led du bouton demarrer
PROG 1 AUT M3 AUTON BOARD 1 GPIO 3 0 ACT LPO 2 0

// Réinitialise GPIO
PROG 1 AUT M4 AUTON BOARD 1 GPIO 3 0 ACT GPIO 3 0

// Passage en mode manuel
PROG 1 AUT M5 AUTON BOARD 1 GPIO 3 0 ACT MANUAL

// Réinitialise GPIO
PROG 1 AUT M6 AUTON BOARD 1 GPIO 1 0 ACT GPIO 1 0

///////////////////////////////////////////////////////////////////////
// CARTE 2 pour la voie du tramway et les 2 voies temporaire Faller AMS
///////////////////////////////////////////////////////////////////////

// Passe en vitesse sur nulle toutes les voies de la carte
PROG 2 AUT M1 AUTON BOARD 2 GPIO 3 0 ACT TRACK 31 SPEED 0 FORW INERTIA 0

// Allume la led rouge du mode manuel
PROG 2 AUT M2 AUTON BOARD 2 GPIO 3 0 ACT LPO 3 1

// Eteint la led du bouton demarrer
PROG 2 AUT M3 AUTON BOARD 2 GPIO 3 0 ACT LPO 2 0

// Réinitialise GPIO
PROG 2 AUT M4 AUTON BOARD 2 GPIO 3 0 ACT GPIO 3 0

// Passage en mode manuel(0)
PROG 2 AUT M5 AUTON BOARD 2 GPIO 3 0 ACT MANUAL0

// Réinitialise GPIO
PROG 2 AUT M6 AUTON BOARD 2 GPIO 1 0 ACT GPIO 1 0

// Eteint affichage des LEDs du TCO (position du Tramway sur la voie)
PROG 2 AUT M7 AUTON BOARD 2 GPIO 3 0 ACT LPO 4 0
PROG 2 AUT M8 AUTON BOARD 2 GPIO 3 0 ACT LPO 5 0

// Ferme les barrières
PROG 2 AUT M9 AUTON BOARD 2 GPIO 3 0 ACT LPO 0 0 
PROG 2 AUT MA AUTON BOARD 2 GPIO 3 0 ACT LPO 1 1 

//============================================================
// GESTION DES MODES AUTOMATIQUES
//============================================================

/////////////////////////////////////////////////////////

// CARTE MASTER 31 pour les 4 voies externes A0,B1,C2,D3
// TIMER 1 => Temps d'attente en gare
// TIMER 2 => Temps de circulation sur les voies
// TIMER 3 => Désactivation dételage
////////////////////////////////////////////////////////

// Le passage en mode automatique va réinitialiser tous les timers
PROG 31 AUT A0 AUTON BOARD 31 GPIO 3 1 ACT AUTOMATIC

// Eteint la led rouge du bouton mode manuel 
PROG 31 AUT A1 AUTON BOARD 31 GPIO 3 1 ACT LPO 3 0

// Allume la led du bouton démarrer
PROG 31 AUT A2 AUTON BOARD 31 GPIO 1 1 ACT LPO 2 1

// Lance la séquence automatique programmée

// DESCRIPTION DE LA SEQUENCE
// --------------------------
// Sur appui bouton démarrer, le train va effectuer des tours pendant 10 unités de temps timer (environ 1mn20),
// puis s'arrêter en gare sur la même période avant de recommencer

// Arme TIMER 1 pour lancer le cycle de démarrage sur les voies A0, B1, C2 et D3
PROG 31 AUT A3 AUTOFF BOARD 31 GPIO 1 1 ACT TIMER 1 1

// Démarre le train voie A0, B1, C2 et D3 sur déclenchement TIMER 1 selon valeurs de consigne du TCO
PROG 31 AUT A4 AUTOFF BOARD 31 TIMER 1 ACT TRACK 31 SPEED KNOB0 FORW INERTIA KNOB1

// Passage du feu au vert
PROG 31 AUT A5 AUTOFF BOARD 31 TIMER 1 ACT LPO 1 0
PROG 31 AUT A6 AUTOFF BOARD 31 TIMER 1 ACT LPO 0 1

// Arme TIMER 2 pour faire tourner le train pendant 10 unités
PROG 31 AUT A7 AUTOFF BOARD 31 TIMER 1 ACT TIMER 2 10

// Désactive l'arret en gare sur détection de train voie A0
PROG 31 AUT A8 AUTOFF BOARD 31 TIMER 1 ACT AUTOFF D1

//Désactive Le passage du feu au rouge sur détection de train voie A0
PROG 31 AUT A9 AUTOFF BOARD 31 TIMER 1 ACT AUTOFF D2
PROG 31 AUT AA AUTOFF BOARD 31 TIMER 1 ACT AUTOFF D3

// Réactive l'arret en gare sur déclenchement du TIMER 2
PROG 31 AUT AB AUTOFF BOARD 31 TIMER 2 ACT AUTON D1

// Réactive le passage du feu au rouge sur détection de train voie A0
PROG 31 AUT AC AUTOFF BOARD 31 TIMER 2 ACT AUTON D2 
PROG 31 AUT AD AUTOFF BOARD 31 TIMER 2 ACT AUTON D3

// Si le train est détecté sur la voie C2 car il a reçu trop tard la commande d'arrêt, il faut le remettre en route pour un tour
PROG 31 AUT AE AUTOFF BOARD 31 TRACK 2 STA ONTRACK ACT TRACK 31 SPEED KNOB0 FORW INERTIA KNOB1

// Réarme le TIMER 1 pour une attente en gare de 10 unités
PROG 31 AUT AF AUTOFF BOARD 31 TIMER 2 ACT TIMER 1 10

// Commande pouvant être désactivée
// --------------------------------

// Arret en gare sur détection voie A0, les voies A0, B1, C2 et D3 sont progressivement arrêtées
PROG 31 AUT D1 AUTOFF BOARD 31 TRACK 0 STA ONTRACK ACT TRACK 31 SPEED 0 FORW INERTIA KNOB1

// Passage du feu au rouge sur détection de train voie A0
PROG 31 AUT D2 AUTOFF BOARD 31 TRACK 0 STA ONTRACK ACT LPO 1 1
PROG 31 AUT D3 AUTOFF BOARD 31 TRACK 0 STA ONTRACK ACT LPO 0 0


/////////////////////////////////////////////////////////////////////
// CARTE 0 pour les 4 voies internes E0,F1,G2,H3
//TIMER 1 => mode normal de la voie
/////////////////////////////////////////////////////////////////////

// Le passage en mode automatique va réinitialiser tous les timers
PROG 0 AUT A0 AUTON BOARD 0 GPIO 3 1 ACT AUTOMATIC

// Eteint la led rouge du bouton mode manuel 
PROG 0 AUT A1 AUTON BOARD 0 GPIO 3 1 ACT LPO 3 0

// Allume la led du bouton démarrer
PROG 0 AUT A2 AUTOFF BOARD 0 GPIO 1 1 ACT LPO 2 1

// Lance la séquence automatique programmée 

// DESCRIPTION DE LA SEQUENCE
// --------------------------
// Sur appui bouton démarrer, le train va effectuer des tours jusqu'à ce que le train à crémaillère quitte la gare du haut, afin de permettre à ce dernier de faire un retournement de la locomotive sur les voies internes. Le redémarrage du train s'effectuera quand le train à crémaillère sera revenu en gare centrale. 

// Mode normal de la carte
PROG 0 AUT A3 AUTOFF BOARD 0 GPIO 1 1 ACT TIMER 1 1

// Démarre le train voie E0, F1, G2 et H3
PROG 0 AUT A4 AUTOFF BOARD 0 TIMER 1 ACT TRACK 31 SPEED KNOB0 FORW INERTIA KNOB1

// Passage du feu au vert
PROG 0 AUT A5 AUTOFF BOARD 0 TIMER 1 ACT LPO 1 0
PROG 0 AUT A6 AUTOFF BOARD 0 TIMER 1 ACT LPO 0 1

// Désactive l'arret en gare sur détection de train voie F1
PROG 0 AUT A7 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D1

//Désactive Le passage du feu au rouge sur détection de train voie F1
PROG 0 AUT A8 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D2
PROG 0 AUT A9 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D3

// Désactive la relance du train sur détection voie H3
PROG 0 AUT AA AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D4

// Désactivation des commandes pour le train à crémaillère
PROG 0 AUT AB AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D5
PROG 0 AUT AC AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D6
PROG 0 AUT AD AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D7
PROG 0 AUT AE AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D8
PROG 0 AUT AF AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D9
PROG 0 AUT AG AUTOFF BOARD 0 TIMER 1 ACT AUTOFF DA
PROG 0 AUT AG AUTOFF BOARD 0 TIMER 1 ACT AUTOFF DB
PROG 0 AUT AG AUTOFF BOARD 0 TIMER 1 ACT AUTOFF DC


/////////////////////////////////////////////////////////////////////////////////////////////////////////
// Ces différentes commandes sont pilotées depuis la carte 1 Crémaillère au travers de TIMER 6 (lorsque le train à crémaillère repart de la gare du haut) 
// Active l'arret en gare sur détection de train voie F1 
PROG 0 AUT B0 AUTOFF BOARD 1 TIMER 6 ACT AUTON D1

// Active le passage du feu au rouge sur détection de train voie F1 
PROG 0 AUT B1 AUTOFF BOARD 1 TIMER 6 ACT AUTON D2
PROG 0 AUT B2 AUTOFF BOARD 1 TIMER 6 ACT AUTON D3

// Active la relance du train sur détection voie H3 
PROG 0 AUT B3 AUTOFF BOARD 1 TIMER 6 ACT AUTON D4

// Désactive la relance du train sur détection voie H3, car le train à crémaillère passe par là
PROG 0 AUT B4 AUTOFF BOARD 1 TIMER 2 ACT AUTOFF D4

// Désactive l'arret du train en gare principale sur détection du train en voie F1, car le train à crémaillère passe par là 
PROG 0 AUT B5 AUTOFF BOARD 1 TIMER 2 ACT AUTOFF D1

// Activation des commandes pour le train à crémaillère
PROG 0 AUT B6 AUTOFF BOARD 1 TIMER 2 ACT AUTON D5
PROG 0 AUT B7 AUTOFF BOARD 1 TIMER 2 ACT AUTON D6
PROG 0 AUT B8 AUTOFF BOARD 1 TIMER 2 ACT AUTON D7
PROG 0 AUT B9 AUTOFF BOARD 1 TIMER 2 ACT AUTON D8
PROG 0 AUT BA AUTOFF BOARD 1 TIMER 2 ACT AUTON D9


/////////////////////////////////////////////////////////////////////////////////////////////////////////

// Commande pouvant être désactivée
// --------------------------------

// => Gestion automatique décrochage circuit caché et aiguillage E0 / (E0) H1 et E0 / G2, voir les commandes automatiques du TCO 

/////////////////////////////////////////////////////////////////////////////////////////////////////////
// Gestion normal de la carte pour arret du train en gare principale
// Arret en gare, les voies E0, F1, G2 et H3 sont progressivement arrêtées sur détection de train voie F1
PROG 0 AUT D1 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT TRACK 31 SPEED 0 FORW INERTIA KNOB1

// Passage du feu au rouge sur détection de train voie F1
PROG 0 AUT D2 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT LPO 1 1
PROG 0 AUT D3 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT LPO 0 0

// Si le train est détecté sur la voie H3 car il a reçu trop tard la commande d'arrêt, il faut le remettre en route pour un tour
PROG 0 AUT D4 AUTOFF BOARD 0 TRACK 3 STA ONTRACK ACT TRACK 31 SPEED KNOB0 FORW INERTIA KNOB1
/////////////////////////////////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////////////////////////////////
// Gestion de la carte pour le train à crémaillère

// Démarrage train à crémaillère vers décrochage partie caché
PROG 0 AUT D5 AUTOFF BOARD 1 TIMER 2 ACT TRACK 30 SPEED 7 BACK INERTIA 15

// Lorsque le train à crémaillère arrive sur la piste 3 on active le décrochage (voir TCO), on modifie les aiguillages (voir TCO), on inverse le sens de la locomotive
PROG 0 AUT D6 AUTOFF BOARD 0 TRACK 3 STA ONTRACK ACT TRACK 30 SPEED 7 FORW INERTIA 50

// On active le changement de sens lorsque le train à crémaillère revient sur la piste 2
PROG 0 AUT D7 AUTOFF BOARD 0 TRACK 3 STA ONTRACK ACT AUTON DA
PROG 0 AUT D8 AUTOFF BOARD 0 TRACK 3 STA ONTRACK ACT AUTON DB
PROG 0 AUT D9 AUTOFF BOARD 0 TRACK 3 STA ONTRACK ACT AUTON DC

// Changement de sens locomotive sur piste 2
PROG 0 AUT DA AUTOFF BOARD 0 TRACK 2 STA ONTRACK ACT TRACK 30 SPEED 7 BACK INERTIA 50

// On désactive le mécanisme de gestion de l'aller-retour
PROG 0 AUT DB AUTOFF BOARD 0 TRACK 2 STA ONTRACK ACT AUTOFF D7
PROG 0 AUT DC AUTOFF BOARD 0 TRACK 2 STA ONTRACK ACT AUTOFF DA

/////////////////////////////////////////////////////////
// CARTE 1 pour les 4 voies de la crémaillère I0,J1,K2,L3
// TIMER 1 => Attente gare du haut avant décrochage locomotive
// TIMER 2 => démarrage du train à crémaillère vers les voies centrales pour retournement
// TIMER 3 => mode normal de la voie
// TIMER 4 => démarrage retour arrière locomotive
// TIMER 5 => démarrage retour avant pour accrochage locomotive
// TIMER 6 => Retour vers la gare centrale
// TIMER 7 => Change le sens de la voie en gare principale après départ du train vers la voie 0
/////////////////////////////////////////////////////////

// Le passage en mode automatique va réinitialiser tous les timers
PROG 1 AUT A0 AUTON BOARD 1 GPIO 3 1 ACT AUTOMATIC

// Eteint la led rouge du bouton mode manuel 
PROG 1 AUT A1 AUTON BOARD 1 GPIO 3 1 ACT LPO 3 0

// Allume la led du bouton démarrer
PROG 1 AUT A2 AUTOFF BOARD 1 GPIO 1 1 ACT LPO 2 1

// Lance la séquence automatique programmée 

// DESCRIPTION DE LA SEQUENCE
// -------------------------
// Sur appui bouton démarrer, le train à crémaillère quitte la gare principale pour se rendre à la gare du haut. Après un temps d'attente de 5 unités, la locomotive se decroche et se positionne à l'arrière du train pour revenir vers la gare centrale. A l'arrivée à la gare centrale, après un temps d'attente de 5 unités, le train à crémaillère va sur la voie interne pour un décrochage de la locomotive et positionnement à l'avant du train en voie caché avant de revenir en gare centrale puis remonter directement à la gare du haut

// Arme TIMER 3 pour déclenchement mode normal automatique
PROG 1 AUT A3 AUTOFF BOARD 1 GPIO 1 1 ACT TIMER 3 1

// Positionne correctement les aiguillages du haut
PROG 1 AUT A4 AUTOFF BOARD 1 TIMER 3 ACT LPO 4 0

// Demarre le train (qui doit être quelque part sur la voie I0 ou L3 (gare) )
PROG 1 AUT A5 AUTOFF BOARD 1 TIMER 3 ACT TRACK 31 SPEED 8 FORW INERTIA 20

// Active détection arrivée en gare du haut et activation timer
PROG 1 AUT A6 AUTOFF BOARD 1 TIMER 3 ACT AUTON D2
PROG 1 AUT A8 AUTOFF BOARD 1 TIMER 3 ACT AUTON D3

// Désactive arret du train en gare centrale 
PROG 1 AUT A7 AUTOFF BOARD 1 TIMER 3 ACT AUTOFF D1

// Désactive armement TIMER 2 pour décrochage bas
PROG 1 AUT A9 AUTOFF BOARD 1 TIMER 3 ACT AUTOFF D4

// Désactive Détection arrivée en gare du haut et activation timer
PROG 1 AUT AA AUTOFF BOARD 1 TIMER 1 ACT AUTOFF D2
PROG 1 AUT AB AUTOFF BOARD 1 TIMER 1 ACT AUTOFF D3

// Démarrage lent pour décrochage wagon 
PROG 1 AUT AC AUTOFF BOARD 1 TIMER 1 ACT TRACK 31 SPEED 4 FORW INERTIA 80

// Arme TIMER 4 pour retour arrière locomotive
PROG 1 AUT AD AUTOFF BOARD 1 TIMER 1 ACT TIMER 4 2

// retour arrière locomotive sur déclenchement TIMER 4
PROG 1 AUT AE AUTOFF BOARD 1 TIMER 4 ACT TRACK 31 SPEED 5 BACK INERTIA 10

// changement aiguillage sur déclenchement TIMER 4
PROG 1 AUT AF AUTOFF BOARD 1 TIMER 4 ACT LPO 4 1

// Arme TIMER 5 pour retour avant locomotive 
PROG 1 AUT AG AUTOFF BOARD 1 TIMER 4 ACT TIMER 5 2

//Retour avant de la locomotive au déclenchement TIMER 5
PROG 1 AUT AH AUTOFF BOARD 1 TIMER 5 ACT TRACK 31 SPEED 4 FORW INERTIA 20

// changement aiguillage sur déclenchement TIMER 5
PROG 1 AUT AI AUTOFF BOARD 1 TIMER 5 ACT LPO 4 0

// Arme TIMER 6 pour retour final locomotive
PROG 1 AUT AJ AUTOFF BOARD 1 TIMER 5 ACT TIMER 6 2

// Retour du train vers la gare centrale (voie I0, J1, K2 et L3)
PROG 1 AUT AK AUTOFF BOARD 1 TIMER 6 ACT TRACK 31 SPEED 5 BACK INERTIA 15

// Active arret du train en gare centrale 
PROG 1 AUT AL AUTOFF BOARD 1 TIMER 6 ACT AUTON D1

// Active armement TIMER 2 pour décrochage bas
PROG 1 AUT AM AUTOFF BOARD 1 TIMER 6 ACT AUTON D4

////////////////////////////////////////////////////////////////////////
// Démarre le train qui passe sur la CARTE 0
PROG 1 AUT AN AUTOFF BOARD 1 TIMER 2 ACT TRACK 31 SPEED 7 BACK INERTIA 15

// Arme TIMER 3 pour remise en mode normal de la voie
PROG 1 AUT AP AUTOFF BOARD 1 TIMER 2 ACT TIMER 3 20

// Arme TIMER 7 pour changer le sens de la voie en gare centrale après le départ du train vers la voie 0
PROG 1 AUT AQ AUTOFF BOARD 1 TIMER 2 ACT TIMER 7 1

// Change le sens de la voie en gare centrale après le départ du train vers la voie 0
PROG 1 AUT AR AUTOFF BOARD 1 TIMER 7 ACT TRACK 31 SPEED 7 FORW INERTIA 0

////////////////////////////////////////////////////////////////////////

// Commande pouvant être désactivée
// --------------------------------

// Arret du train lors de l'arrivée en gare centrale (voie I0, J1, K2 et L3)
PROG 1 AUT D1 AUTOFF BOARD 1 TRACK 0 STA ONTRACK ACT TRACK 31 SPEED 0 BACK INERTIA 100

// Détection du train sur la voie K, arret des voies I0, J1, K2 et L3
PROG 1 AUT D2 AUTOFF BOARD 1 TRACK 2 STA ONTRACK ACT TRACK 31 SPEED 0 FORW INERTIA 20

// Arme TIMER 1 pour attente en gare du haut pendant 5 unités 
PROG 1 AUT D3 AUTOFF BOARD 1 TRACK 2 STA ONTRACK ACT TIMER 1 5 

// Arme TIMER 2 pour séquence de décrochage vers la partie cachée
PROG 1 AUT D4 AUTOFF BOARD 1 TRACK 0 STA ONTRACK ACT TIMER 2 5

///////////////////////////////////////////////////////////////////////
// CARTE 2 pour la voie 0 du tramway et les 2 voies temporaire Faller AMS
// TIMER 1 => Temps d'attente devant les gares principales et intermédiaires à l'aller
// TIMER 2 => Temps d'attente à la gare devant la grue 
// TIMER 3 => Temps d'attente à la gare intermédiaire au retour
///////////////////////////////////////////////////////////////////////

// Le passage en mode automatique va réinitialiser tous les timers
PROG 2 AUT A0 AUTON BOARD 2 GPIO 3 1 ACT AUTOMATIC

// Eteint la led rouge du bouton mode manuel 
PROG 2 AUT A1 AUTON BOARD 2 GPIO 3 1 ACT LPO 3 0

// Allume la led du bouton démarrer
PROG 2 AUT A2 AUTOFF BOARD 2 GPIO 1 1 ACT LPO 2 1

// Lance la séquence automatique programmée 

// DESCRIPTION DE LA SEQUENCE
// --------------------------
// Sur appui bouton démarrer, et sous réserve que le Tramway a bien été positionné devant la gare centrale, il va venir se positionner devant la gare du tramway à droite de la gare centrale, puis suivre un cycle d'aller/retour régulier avec un arret à l'aller et au retour à la gare intermédiaire. Chaque arret est programmé sur un timer de 5 unités

// Démarre le tramway pour le ramener sur la droite, le tramway doit être devant la gare de train
PROG 2 AUT A3 AUTOFF BOARD 2 GPIO 1 1 ACT TRACK 0 SPEED 5 BACK INERTIA 27

// Réinitialise GPIO 2 pour comptage position tramway
PROG 2 AUT A4 AUTOFF BOARD 2 GPIO 1 1 ACT GPIO 2 0

// Démarre les voies de voiture (temporaire)
PROG 2 AUT A5 AUTOFF BOARD 2 GPIO 1 1 ACT TRACK 1 SPEED 5 FORW INERTIA 1
PROG 2 AUT A6 AUTOFF BOARD 2 GPIO 1 1 ACT TRACK 2 SPEED 6 FORW INERTIA 1

// Allume led TCO droite M0
PROG 2 AUT A7 AUTOFF BOARD 2 GPIO 2 2 ACT LPO 5 1

// Stop le tramway 
PROG 2 AUT A8 AUTOFF BOARD 2 GPIO 2 2 ACT TRACK 0 SPEED 0 BACK INERTIA 27

// Arme timer 1 d'attente en gare
PROG 2 AUT A9 AUTOFF BOARD 2 GPIO 2 2 ACT TIMER 1 5

// Redémarre le tramway sur déclenchement timer 1
PROG 2 AUT AA AUTOFF BOARD 2 TIMER 1 ACT TRACK 0 SPEED 5 FORW INERTIA 27

// Eteint led TCO droite M0
PROG 2 AUT AB AUTOFF BOARD 2 GPIO 2 4 ACT LPO 5 0

// Gare intermédiaire
PROG 2 AUT AC AUTOFF BOARD 2 GPIO 2 6 ACT TRACK 0 SPEED 0 FORW INERTIA 27

// Arme timer 1 d'attente en gare intermédiaire
PROG 2 AUT AD AUTOFF BOARD 2 GPIO 2 6 ACT TIMER 1 5

// ferme les barrieres
PROG 2 AUT AE AUTOFF BOARD 2 GPIO 2 8 ACT LPO 0 0 
PROG 2 AUT AF AUTOFF BOARD 2 GPIO 2 8 ACT LPO 1 1 

PROG 2 AUT AG AUTOFF BOARD 2 TIMER 2 ACT LPO 0 0 
PROG 2 AUT AH AUTOFF BOARD 2 TIMER 2 ACT LPO 1 1 

// ouvre les barrieres
PROG 2 AUT AI AUTOFF BOARD 2 GPIO 2 12 ACT LPO 0 1
PROG 2 AUT AJ AUTOFF BOARD 2 GPIO 2 12 ACT LPO 1 0

PROG 2 AUT AK AUTOFF BOARD 2 GPIO 2 20 ACT LPO 0 1
PROG 2 AUT AL AUTOFF BOARD 2 GPIO 2 20 ACT LPO 1 0

PROG 2 AUT AM AUTOFF BOARD 2 GPIO 2 2 ACT LPO 0 1
PROG 2 AUT AN AUTOFF BOARD 2 GPIO 2 2 ACT LPO 1 0

// Stop le tramway 
PROG 2 AUT AO AUTOFF BOARD 2 GPIO 2 14 ACT TRACK 0 SPEED 0 FORW INERTIA 27

// Allume led TCO gauche M0
PROG 2 AUT AP AUTOFF BOARD 2 GPIO 2 14 ACT LPO 4 1

// Arme timer 2 d'attente en gare
PROG 2 AUT AQ AUTOFF BOARD 2 GPIO 2 14 ACT TIMER 2 5

// Redémarre le tramway sur déclenchement timer 2
PROG 2 AUT AR AUTOFF BOARD 2 TIMER 2 ACT TRACK 0 SPEED 5 BACK INERTIA 27

// Eteint led TCO gauche M0
PROG 2 AUT AS AUTOFF BOARD 2 GPIO 2 16 ACT LPO 4 0

// Gare intermédiaire retour
PROG 2 AUT AT AUTOFF BOARD 2 GPIO 2 22 ACT TRACK 0 SPEED 0 FORW INERTIA 27

// Arme timer 3 d'attente en gare intermédiaire
PROG 2 AUT AU AUTOFF BOARD 2 GPIO 2 22 ACT TIMER 3 5

// Redémarre le tramway sur déclenchement timer 3
PROG 2 AUT AV AUTOFF BOARD 2 TIMER 3 ACT TRACK 0 SPEED 5 BACK INERTIA 27

// Réinitialise GPIO 2 pour nouveau cycle aller-retour
PROG 2 AUT AW AUTOFF BOARD 2 GPIO 2 24 ACT GPIO 2 0

///////////////////////////////////////////////////////////////////////
// CARTE 3 AIGUILLAGES
///////////////////////////////////////////////////////////////////////

PROG 3 AUT M1 AUTON BOARD 3 GPIO 0 0 ACT GPIO 0 0
PROG 3 AUT M2 AUTON BOARD 3 GPIO 1 0 ACT GPIO 1 0
PROG 3 AUT M3 AUTON BOARD 3 GPIO 2 0 ACT GPIO 2 0
PROG 3 AUT M4 AUTON BOARD 3 GPIO 3 0 ACT GPIO 3 0

//============================================================
// GESTION DES COMMANDES DU TCO EN MODE AUTOMATIQUE
//============================================================

// Pilotage automatique décrochage circuit caché
PROG 31 AUT T1 AUTON BOARD 0 TRACK 3 STA ONTRACK ACT LPO 4 1
PROG 31 AUT T2 AUTON BOARD 1 TIMER 2 ACT AUTON T1
PROG 31 AUT T3 AUTON BOARD 0 TIMER 1 ACT AUTOFF T1
PROG 31 AUT T4 AUTON BOARD 1 TIMER 3 ACT AUTOFF T1
PROG 31 AUT T5 AUTON BOARD 0 TRACK 2 STA ONTRACK ACT LPO 4 0
PROG 31 AUT T6 AUTON BOARD 0 TRACK 3 STA ONTRACK ACT AUTOFF T1


// Commande automatique aiguillage E0 / (E0) H1 et E0 / G2
PROG 1 AUT T1 AUTON BOARD 0 TRACK 3 STA ONTRACK ACT LPO 0 1
PROG 1 AUT T2 AUTON BOARD 1 TIMER 2 ACT AUTON T1
PROG 1 AUT T3 AUTON BOARD 0 GPIO 1 1 ACT AUTOFF T1
PROG 1 AUT T4 AUTON BOARD 1 GPIO 1 1 ACT AUTOFF T1
PROG 1 AUT T5 AUTON BOARD 0 TRACK 2 STA ONTRACK ACT LPO 0 0
PROG 1 AUT T6 AUTON BOARD 0 TRACK 3 STA ONTRACK ACT AUTOFF T1

// Pilotage automatique décrochage gare crémaillère haut
PROG 0 AUT T1 AUTON BOARD 1 TIMER 1 ACT LPO 4 1 
PROG 0 AUT T2 AUTON BOARD 1 TIMER 4 ACT LPO 4 0

PROG 3 AUT T1 AUTON BOARD 1 TIMER 2 ACT LPO 1 0
PROG 3 AUT T2 AUTON BOARD 1 TIMER 2 ACT LPO 0 1

// Commande automatique B1 / CROSS
PROG 3 AUT T3 AUTON BOARD 31 GPIO 1 1 ACT LPO 2 1
PROG 3 AUT T4 AUTON BOARD 31 GPIO 1 1 ACT LPO 3 0
PROG 3 AUT T5 AUTON BOARD 31 GPIO 1 1 ACT LPO 2 1
PROG 3 AUT T6 AUTON BOARD 31 GPIO 1 1 ACT LPO 3 0

// Commande automatique D3 / H3 et C2 / G2
PROG 3 AUT T7 AUTON BOARD 31 GPIO 1 1 ACT LPO 5 0
PROG 3 AUT T8 AUTON BOARD 0 GPIO 1 1 ACT LPO 5 0

// Commande automatique CROSS
PROG 3 AUT T9 AUTON BOARD 0 TIMER 1 ACT LPO 1 1
PROG 3 AUT TA AUTON BOARD 0 TIMER 1 ACT LPO 0 0

PROG 3 AUT TB AUTON BOARD 1 TIMER 2 ACT LPO 1 0
PROG 3 AUT TC AUTON BOARD 1 TIMER 2 ACT LPO 0 1

//============================================================
// GESTION DES COMMANDES DU TCO EN MODE MANUEL 
//============================================================

// Pilotage manuel décrochage circuit caché
PROG 31 AUT T7 AUTON BOARD 3 GPIO 3 1 ACT LPO 4 1
PROG 31 AUT T8 AUTON BOARD 3 GPIO 3 0 ACT LPO 4 0

// Remise à zero GPIO
PROG 31 AUT T9 AUTON BOARD 31 GPIO 0 0 ACT GPIO 2 0
PROG 31 AUT TA AUTON BOARD 31 GPIO 2 0 ACT GPIO 0 0

// Pilotage manuel décrochage gare crémaillère haut
PROG 0 AUT T3 AUTON BOARD 3 GPIO 2 1 ACT LPO 4 1
PROG 0 AUT T4 AUTON BOARD 3 GPIO 2 0 ACT LPO 4 0

// Commande manuel D3 / H3 et C2 / G2
PROG 0 AUT T5 AUTON BOARD 0 GPIO 0 0 ACT GPIO 0 0
PROG 0 AUT T6 AUTON BOARD 0 GPIO 2 0 ACT GPIO 2 0

// Commande manuel aiguillage E0 / (E0) H1 et E0 / G2
PROG 1 AUT T7 AUTON BOARD 1 GPIO 2 1 ACT LPO 0 1
PROG 1 AUT T8 AUTON BOARD 1 GPIO 0 1 ACT LPO 0 0
PROG 1 AUT T9 AUTON BOARD 1 GPIO 2 0 ACT GPIO 2 0
PROG 1 AUT TA AUTON BOARD 1 GPIO 0 0 ACT GPIO 0 0

// Commande manuel aiguillage I0 / J1 , K2
PROG 1 AUT TB AUTON BOARD 3 GPIO 0 1 ACT LPO 4 1
PROG 1 AUT TC AUTON BOARD 3 GPIO 1 1 ACT LPO 4 0

// Commande manuel CROSS
PROG 3 AUT TD AUTON BOARD 31 GPIO 0 1 ACT LPO 1 1
PROG 3 AUT TE AUTON BOARD 31 GPIO 2 1 ACT LPO 0 1
PROG 3 AUT TF AUTON BOARD 31 GPIO 2 1 ACT LPO 1 0
PROG 3 AUT TG AUTON BOARD 31 GPIO 0 1 ACT LPO 0 0

// Commande manuel D3 / H3 et C2 / G2
PROG 3 AUT TH AUTON BOARD 0 GPIO 0 1 ACT LPO 5 1
PROG 3 AUT TI AUTON BOARD 0 GPIO 2 1 ACT LPO 5 0

// Commande manuel B1 / CROSS
PROG 3 AUT TJ AUTON BOARD 31 GPIO 0 2 ACT LPO 2 1
PROG 3 AUT TK AUTON BOARD 31 GPIO 2 2 ACT LPO 3 1
PROG 3 AUT TL AUTON BOARD 31 GPIO 2 2 ACT LPO 2 0
PROG 3 AUT TM AUTON BOARD 31 GPIO 0 2 ACT LPO 3 0
