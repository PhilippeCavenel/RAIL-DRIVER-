//================================================//
// GESTION DES AUTOMATISMES DE LA CARTE 0         //
//================================================//

// Connect Rail 0	Voie E
// Connect Rail 1	Voie F
// Connect Rail 2	Voie G
// Connect Rail 3	Voie H

// LPO 0		Feu vert E
// LPO 1		Feu rouge E
// LPO 2		Led bouton blanc
// LPO 3		Led mode manuel
// LPO 4		non connecté
// LPO 5		voie CROSS sur voie F (0) voie CROSS sur voie à définir (non connecté)

// GPIO input 0		Bouton aiguillage D3 / H3 et C2/ G2 rouge
// GPIO input 1		Bouton blanc démarrer
// GPIO input 2		Bouton aiguillage D3 / H3 et C2/ G2 noir
// GPIO input 3		Contact mode manuel

// Track Status	0	Afficheur 7 segments DIO
// Track Status	1	Afficheur 7 segments CLK
// Track Status	2	Potentiomètre inertie	
// Track Status	3	Potentiomètre vitesse

//////////////////////////////////////////////////////////////////////////
// MODE MANUEL 
//////////////////////////////////////////////////////////////////////////

// Détection commutation en mode manuel et activation du mode Manuel
PROG 0 AUT M0 AUTON BOARD 0 GPIO 3 0 ACT MANUAL

// Détection commutation en mode manuel et éteint la LED bleue
PROG 0 AUT M1 AUTON BOARD 0 GPIO 3 0 ACT LPO 2 0

// Détection commutation en mode manuel et passe le feu au vert 
PROG 0 AUT M3 AUTON BOARD 0 GPIO 3 0 ACT LPO 1 0
PROG 0 AUT M4 AUTON BOARD 0 GPIO 3 0 ACT LPO 0 1

// Détection commutation en mode manuel et allume led mode manuel
PROG 0 AUT M5 AUTON BOARD 0 GPIO 3 0 ACT LPO 3 1

// Détection commutation en mode manuel et remise à zéro du GPIO 3 en mode manuel
PROG 0 AUT M6 AUTON BOARD 0 GPIO 3 0 ACT GPIO 3 0

//////////////////////////////////////////////////////////////////////////
// MODE AUTOMATIQUE 
//////////////////////////////////////////////////////////////////////////

// Détection commutation en mode automatique et activation du mode automatique
PROG 0 AUT A1 AUTON BOARD 0 GPIO 3 1 ACT AUTOMATIC

// Détection commutation en mode automatique et désactive armement du timer 1
PROG 0 AUT A2 AUTON BOARD 0 GPIO 3 1 ACT AUTOFF D4

// Détection commutation en mode automatique et désactive armement du timer 2 
PROG 0 AUT A3 AUTON BOARD 0 GPIO 3 1 ACT AUTOFF T4

// Détection commutation en mode automatique éteint led mode manuel
PROG 0 AUT A4 AUTON BOARD 0 GPIO 3 1 ACT LPO 3 0

//////////////////////////////////////////////////////////////////////////
// DETECTION DU TRAIN SUR LA VOIE F MODE AUTOMATIQUE
/////////////////////////////////////////////////////////////////////////

// Passage du feu au rouge sur détection de train voie F
PROG 0 AUT D1 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT LPO 1 1
PROG 0 AUT D2 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT LPO 0 0

// Arret en gare, les voies E, F, G et H sont progressivement arrêtées
PROG 0 AUT D3 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT TRACK 4 SPEED 0 FORW INERTIA KNOB1

// Arme TIMER 1 pour attente en gare pendant 10 unités
PROG 0 AUT D4 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT TIMER 1 10

// Si le train est détecté sur la voie 3 il faut le remettre en route
// Sans doute une détection tardive d'un wagon sur la voie 1
PROG 0 AUT D5 AUTOFF BOARD 0 TRACK 3 STA ONTRACK ACT TRACK 4 SPEED KNOB0 FORW INERTIA KNOB1

//////////////////////////////////////////////////////////////////////////
// TIMER 1 se déclenche MODE AUTOMATIQUE
//////////////////////////////////////////////////////////////////////////

// Démarre le train voie E, F, G et H
PROG 0 AUT T1 AUTOFF BOARD 0 TIMER 1 ACT TRACK 4 SPEED KNOB0 FORW INERTIA KNOB1

// Passage du feu au vert
PROG 0 AUT T2 AUTOFF BOARD 0 TIMER 1 ACT LPO 1 0
PROG 0 AUT T3 AUTOFF BOARD 0 TIMER 1 ACT LPO 0 1

// Arme TIMER 2 pour faire tourner le train pendant 10 unités
PROG 0 AUT T4 AUTOFF BOARD 0 TIMER 1 ACT TIMER 2 10

// Désactive l'arret en gare sur détection de train voie F
PROG 0 AUT T5 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D3

//Désactive Le passage du feu au rouge sur détection de train voie F
PROG 0 AUT T6 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D1
PROG 0 AUT T7 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D2

// Désactive armement timer 1
PROG 0 AUT T8 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D4

//////////////////////////////////////////////////////////////////////////
// TIMER 2 se déclenche MODE AUTOMATIQUE
//////////////////////////////////////////////////////////////////////////

// Active l'arret en gare sur détection de train voie F
PROG 0 AUT T9 AUTOFF BOARD 0 TIMER 2 ACT AUTON D3

// Active le passage du feu au rouge sur détection de train voie F
PROG 0 AUT TA AUTOFF BOARD 0 TIMER 2 ACT AUTON D1 
PROG 0 AUT TB AUTOFF BOARD 0 TIMER 2 ACT AUTON D2

// Active armement timer 1
PROG 0 AUT TC AUTOFF BOARD 0 TIMER 2 ACT AUTON D4

//////////////////////////////////////////////////////////////////////////
// Déclenchement du MOD AUTOMATIQUE sur appui bouton blanc carte 0 
//////////////////////////////////////////////////////////////////////////

// Arme TIMER 1 pour lancer le cycle de démarrage sur les voies E, F, G et H
PROG 0 AUT B1 AUTOFF BOARD 0 GPIO 1 1 ACT TIMER 1 1

// Active le réarmement du TIMER 1
PROG 0 AUT B3 AUTOFF BOARD 0 GPIO 1 1 ACT AUTON D4

// Active le réarmement du TIMER 2
PROG 0 AUT B4 AUTOFF BOARD 0 GPIO 1 1 ACT AUTON T4

// Allume la LED blanche du bouton
PROG 0 AUT B5 AUTOFF BOARD 0 GPIO 1 1 ACT LPO 2 1

// Remise à zéro du GPIO 1 sur relanchement bouton
PROG 0 AUT B6 AUTOFF BOARD 0 GPIO 1 0 ACT GPIO 1 0

/////////////////////////////////////////////////

PROG 0 AUT C0 AUTON BOARD 31 GPIO 0 1 ACT LPO 5 0

// Voie non connectée pour le moment
PROG 0 AUT C1 AUTON BOARD 31 GPIO 2 1 ACT LPO 5 1 

PROG 0 AUT C2 AUTON BOARD 0 GPIO 1 1 ACT LPO 5 0
PROG 0 AUT C3 AUTON BOARD 0 GPIO 0 0 ACT GPIO 0 0 
PROG 0 AUT C4 AUTON BOARD 0 GPIO 2 0 ACT GPIO 2 0 
