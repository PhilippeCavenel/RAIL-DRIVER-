//================================================//
// GESTION DES AUTOMATISMES DE LA CARTE MASTER 31 //
//================================================//

// Connect Rail 0	Voie A
// Connect Rail 1	Voie B
// Connect Rail 2	Voie C
// Connect Rail 3	Voie D

// LPO 0		Feu vert B
// LPO 1		Feu rouge B
// LPO 2		Led bouton bleu
// LPO 3		Led mode manuel
// LPO 4		non connecté
// LPO 5		non connecté

// GPIO input 0		Bouton aiguillage CROSS rouge
// GPIO input 1		Bouton bleu démarrer
// GPIO input 2		Bouton aiguillage CROSS noir
// GPIO input 3		Contact mode manuel

// Track Status	0	Afficheur 7 segments DIO
// Track Status	1	Afficheur 7 segments CLK
// Track Status	2	Potentiomètre inertie	
// Track Status	3	Potentiomètre vitesse


//////////////////////////////////////////////////////////////////////////
// MODE MANUEL 
//////////////////////////////////////////////////////////////////////////

// Détection commutation en mode manuel et activation du mode Manuel
PROG 31 AUT M0 AUTON BOARD 31 GPIO 3 0 ACT MANUAL

// Détection commutation en mode manuel et éteint la LED bleue
PROG 31 AUT M1 AUTON BOARD 31 GPIO 3 0 ACT LPO 2 0

// Détection commutation en mode manuel et passe le feu au vert 
PROG 31 AUT M3 AUTON BOARD 31 GPIO 3 0 ACT LPO 1 0
PROG 31 AUT M4 AUTON BOARD 31 GPIO 3 0 ACT LPO 0 1

// Détection commutation en mode manuel et allume led mode manuel
PROG 31 AUT M5 AUTON BOARD 31 GPIO 3 0 ACT LPO 3 1

// Détection commutation en mode manuel et remise à zéro du GPIO 3 en mode manuel
PROG 31 AUT M6 AUTON BOARD 31 GPIO 3 0 ACT GPIO 3 0

//////////////////////////////////////////////////////////////////////////
// MODE AUTOMATIQUE 
//////////////////////////////////////////////////////////////////////////

// Détection commutation en mode automatique et activation du mode automatique
PROG 31 AUT A1 AUTON BOARD 31 GPIO 3 1 ACT AUTOMATIC

// Détection commutation en mode automatique et désactive armement du timer 1
PROG 31 AUT A2 AUTON BOARD 31 GPIO 3 1 ACT AUTOFF D4

// Détection commutation en mode automatique et désactive armement du timer 2 
PROG 31 AUT A3 AUTON BOARD 31 GPIO 3 1 ACT AUTOFF T4

// Détection commutation en mode automatique éteint led mode manuel
PROG 31 AUT A4 AUTON BOARD 31 GPIO 3 1 ACT LPO 3 0

//////////////////////////////////////////////////////////////////////////
// DETECTION DU TRAIN SUR LA VOIE A MODE AUTOMATIQUE
/////////////////////////////////////////////////////////////////////////

// Passage du feu au rouge sur détection de train voie A
PROG 31 AUT D1 AUTOFF BOARD 31 TRACK 0 STA ONTRACK ACT LPO 1 1
PROG 31 AUT D2 AUTOFF BOARD 31 TRACK 0 STA ONTRACK ACT LPO 0 0

// Arret en gare, les voies A, B, C et D sont progressivement arrêtées
PROG 31 AUT D3 AUTOFF BOARD 31 TRACK 0 STA ONTRACK ACT TRACK 4 SPEED 0 FORW INERTIA KNOB1

// Arme TIMER 1 pour attente en gare pendant 10 unités
PROG 31 AUT D4 AUTOFF BOARD 31 TRACK 0 STA ONTRACK ACT TIMER 1 10 

// Si le train est détecté sur la voie 2 il faut le remettre en route
// Sans doute une détection tardive d'un wagon sur la voie 0
PROG 31 AUT D5 AUTOFF BOARD 31 TRACK 2 STA ONTRACK ACT TRACK 4 SPEED KNOB0 FORW INERTIA KNOB1

//////////////////////////////////////////////////////////////////////////
// TIMER 1 se déclenche MODE AUTOMATIQUE
//////////////////////////////////////////////////////////////////////////

// Démarre le train voie A, B, C et D
PROG 31 AUT T1 AUTOFF BOARD 31 TIMER 1 ACT TRACK 4 SPEED KNOB0 FORW INERTIA KNOB1

// Passage du feu au vert
PROG 31 AUT T2 AUTOFF BOARD 31 TIMER 1 ACT LPO 1 0
PROG 31 AUT T3 AUTOFF BOARD 31 TIMER 1 ACT LPO 0 1

// Arme TIMER 2 pour faire tourner le train pendant 10 unités
PROG 31 AUT T4 AUTOFF BOARD 31 TIMER 1 ACT TIMER 2 10

// Désactive l'arret en gare sur détection de train voie A
PROG 31 AUT T5 AUTOFF BOARD 31 TIMER 1 ACT AUTOFF D3

//Désactive Le passage du feu au rouge sur détection de train voie A
PROG 31 AUT T6 AUTOFF BOARD 31 TIMER 1 ACT AUTOFF D1
PROG 31 AUT T7 AUTOFF BOARD 31 TIMER 1 ACT AUTOFF D2

// Désactive armement timer 1
PROG 31 AUT T8 AUTOFF BOARD 31 TIMER 1 ACT AUTOFF D4

//////////////////////////////////////////////////////////////////////////
// TIMER 2 se déclenche MODE AUTOMATIQUE
//////////////////////////////////////////////////////////////////////////

// Active l'arret en gare sur détection de train voie A
PROG 31 AUT T9 AUTOFF BOARD 31 TIMER 2 ACT AUTON D3

// Active le passage du feu au rouge sur détection de train voie A
PROG 31 AUT TA AUTOFF BOARD 31 TIMER 2 ACT AUTON D1 
PROG 31 AUT TB AUTOFF BOARD 31 TIMER 2 ACT AUTON D2

// Active armement timer 1
PROG 31 AUT TC AUTOFF BOARD 31 TIMER 2 ACT AUTON D4

//////////////////////////////////////////////////////////////////////////
// Déclenchement du MOD AUTOMATIQUE sur appui bouton bleu carte 31 
//////////////////////////////////////////////////////////////////////////

// Arme TIMER 1 pour lancer le cycle de démarrage sur les voies A, B, C et D
PROG 31 AUT B1 AUTOFF BOARD 31 GPIO 1 1 ACT TIMER 1 1

// Active le réarmement du TIMER 1
PROG 31 AUT B3 AUTOFF BOARD 31 GPIO 1 1 ACT AUTON D4

// Active le réarmement du TIMER 2
PROG 31 AUT B4 AUTOFF BOARD 31 GPIO 1 1 ACT AUTON T4

// Allume la LED Bleu du bouton
PROG 31 AUT B5 AUTOFF BOARD 31 GPIO 1 1 ACT LPO 2 1

// Remise à zéro du GPIO 1 sur relanchement bouton
PROG 31 AUT B6 AUTOFF BOARD 31 GPIO 1 0 ACT GPIO 1 0

///////////////////////////////////////////////////

PROG 31 AUT C0 AUTON BOARD 31 GPIO 0 1 ACT GPIO 2 0
PROG 31 AUT C1 AUTON BOARD 31 GPIO 2 1 ACT GPIO 0 0

//================================================//
// GESTION DES AUTOMATISMES DE LA CARTE 0         //
//================================================//

// Connect Rail 0	Voie E
// Connect Rail 1	Voie F
// Connect Rail 2	Voie G
// Connect Rail 3	Voie H

// LPO 0		Feu vert E
// LPO 1		Feu rouge E
// LPO 2		Led bouton blanc
// LPO 3		Led mode manuel
// LPO 4		non connecté
// LPO 5		voie CROSS sur voie F (0) voie CROSS sur voie à définir (non connecté)

// GPIO input 0		Bouton aiguillage D3 / H3 et C2/ G2 rouge
// GPIO input 1		Bouton blanc démarrer
// GPIO input 2		Bouton aiguillage D3 / H3 et C2/ G2 noir
// GPIO input 3		Contact mode manuel

// Track Status	0	Afficheur 7 segments DIO
// Track Status	1	Afficheur 7 segments CLK
// Track Status	2	Potentiomètre inertie	
// Track Status	3	Potentiomètre vitesse

//////////////////////////////////////////////////////////////////////////
// MODE MANUEL 
//////////////////////////////////////////////////////////////////////////

// Détection commutation en mode manuel et activation du mode Manuel
PROG 0 AUT M0 AUTON BOARD 0 GPIO 3 0 ACT MANUAL

// Détection commutation en mode manuel et éteint la LED bleue
PROG 0 AUT M1 AUTON BOARD 0 GPIO 3 0 ACT LPO 2 0

// Détection commutation en mode manuel et passe le feu au vert 
PROG 0 AUT M3 AUTON BOARD 0 GPIO 3 0 ACT LPO 1 0
PROG 0 AUT M4 AUTON BOARD 0 GPIO 3 0 ACT LPO 0 1

// Détection commutation en mode manuel et allume led mode manuel
PROG 0 AUT M5 AUTON BOARD 0 GPIO 3 0 ACT LPO 3 1

// Détection commutation en mode manuel et remise à zéro du GPIO 3 en mode manuel
PROG 0 AUT M6 AUTON BOARD 0 GPIO 3 0 ACT GPIO 3 0

//////////////////////////////////////////////////////////////////////////
// MODE AUTOMATIQUE 
//////////////////////////////////////////////////////////////////////////

// Détection commutation en mode automatique et activation du mode automatique
PROG 0 AUT A1 AUTON BOARD 0 GPIO 3 1 ACT AUTOMATIC

// Détection commutation en mode automatique et désactive armement du timer 1
PROG 0 AUT A2 AUTON BOARD 0 GPIO 3 1 ACT AUTOFF D4

// Détection commutation en mode automatique et désactive armement du timer 2 
PROG 0 AUT A3 AUTON BOARD 0 GPIO 3 1 ACT AUTOFF T4

// Détection commutation en mode automatique éteint led mode manuel
PROG 0 AUT A4 AUTON BOARD 0 GPIO 3 1 ACT LPO 3 0

//////////////////////////////////////////////////////////////////////////
// DETECTION DU TRAIN SUR LA VOIE F MODE AUTOMATIQUE
/////////////////////////////////////////////////////////////////////////

// Passage du feu au rouge sur détection de train voie F
PROG 0 AUT D1 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT LPO 1 1
PROG 0 AUT D2 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT LPO 0 0

// Arret en gare, les voies E, F, G et H sont progressivement arrêtées
PROG 0 AUT D3 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT TRACK 4 SPEED 0 FORW INERTIA KNOB1

// Arme TIMER 1 pour attente en gare pendant 10 unités
PROG 0 AUT D4 AUTOFF BOARD 0 TRACK 1 STA ONTRACK ACT TIMER 1 10

// Si le train est détecté sur la voie 3 il faut le remettre en route
// Sans doute une détection tardive d'un wagon sur la voie 1
PROG 0 AUT D5 AUTOFF BOARD 0 TRACK 3 STA ONTRACK ACT TRACK 4 SPEED KNOB0 FORW INERTIA KNOB1

//////////////////////////////////////////////////////////////////////////
// TIMER 1 se déclenche MODE AUTOMATIQUE
//////////////////////////////////////////////////////////////////////////

// Démarre le train voie E, F, G et H
PROG 0 AUT T1 AUTOFF BOARD 0 TIMER 1 ACT TRACK 4 SPEED KNOB0 FORW INERTIA KNOB1

// Passage du feu au vert
PROG 0 AUT T2 AUTOFF BOARD 0 TIMER 1 ACT LPO 1 0
PROG 0 AUT T3 AUTOFF BOARD 0 TIMER 1 ACT LPO 0 1

// Arme TIMER 2 pour faire tourner le train pendant 10 unités
PROG 0 AUT T4 AUTOFF BOARD 0 TIMER 1 ACT TIMER 2 10

// Désactive l'arret en gare sur détection de train voie F
PROG 0 AUT T5 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D3

//Désactive Le passage du feu au rouge sur détection de train voie F
PROG 0 AUT T6 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D1
PROG 0 AUT T7 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D2

// Désactive armement timer 1
PROG 0 AUT T8 AUTOFF BOARD 0 TIMER 1 ACT AUTOFF D4

//////////////////////////////////////////////////////////////////////////
// TIMER 2 se déclenche MODE AUTOMATIQUE
//////////////////////////////////////////////////////////////////////////

// Active l'arret en gare sur détection de train voie F
PROG 0 AUT T9 AUTOFF BOARD 0 TIMER 2 ACT AUTON D3

// Active le passage du feu au rouge sur détection de train voie F
PROG 0 AUT TA AUTOFF BOARD 0 TIMER 2 ACT AUTON D1 
PROG 0 AUT TB AUTOFF BOARD 0 TIMER 2 ACT AUTON D2

// Active armement timer 1
PROG 0 AUT TC AUTOFF BOARD 0 TIMER 2 ACT AUTON D4

//////////////////////////////////////////////////////////////////////////
// Déclenchement du MOD AUTOMATIQUE sur appui bouton blanc carte 0 
//////////////////////////////////////////////////////////////////////////

// Arme TIMER 1 pour lancer le cycle de démarrage sur les voies E, F, G et H
PROG 0 AUT B1 AUTOFF BOARD 0 GPIO 1 1 ACT TIMER 1 1

// Active le réarmement du TIMER 1
PROG 0 AUT B3 AUTOFF BOARD 0 GPIO 1 1 ACT AUTON D4

// Active le réarmement du TIMER 2
PROG 0 AUT B4 AUTOFF BOARD 0 GPIO 1 1 ACT AUTON T4

// Allume la LED blanche du bouton
PROG 0 AUT B5 AUTOFF BOARD 0 GPIO 1 1 ACT LPO 2 1

// Remise à zéro du GPIO 1 sur relanchement bouton
PROG 0 AUT B6 AUTOFF BOARD 0 GPIO 1 0 ACT GPIO 1 0

/////////////////////////////////////////////////

PROG 0 AUT C0 AUTON BOARD 31 GPIO 0 1 ACT LPO 5 0

// Voie non connectée pour le moment
PROG 0 AUT C1 AUTON BOARD 31 GPIO 2 1 ACT LPO 5 1 

PROG 0 AUT C2 AUTON BOARD 0 GPIO 1 1 ACT LPO 5 0
PROG 0 AUT C3 AUTON BOARD 0 GPIO 0 0 ACT GPIO 0 0 
PROG 0 AUT C4 AUTON BOARD 0 GPIO 2 0 ACT GPIO 2 0 

//================================================//
// GESTION DES AUTOMATISMES DE LA CARTE 1         //
//================================================//

// Connect Rail 0	Voie non connectée
// Connect Rail 1	Voie non connectée
// Connect Rail 2	Voie non connectée
// Connect Rail 3	Voie non connectée

// LPO 0		aiguillage CROSS noir
// LPO 1		aiguillage CROSS rouge
// LPO 2		aiguillage B1 / CROSS (noir cmd manuel double appui)
// LPO 3		aiguillage B1 / CROSS (rouge cmd manuel double appui)
// LPO 4		aiguillage D3 / H3 et C2 / G2
// LPO 5		aiguillage D3 / H3 et C2 / G2

// GPIO input 0		non connecté
// GPIO input 1		non connecté
// GPIO input 2		non connecté
// GPIO input 3		non connecté

// Track Status	0	non connecté
// Track Status	1	non connecté
// Track Status	2	non connecté	
// Track Status	3	non connecté

// Commande manuel CROSS
PROG 1 AUT C1 AUTON BOARD 31 GPIO 0 1 ACT LPO 1 1
PROG 1 AUT C2 AUTON BOARD 31 GPIO 2 1 ACT LPO 0 1
PROG 1 AUT C3 AUTON BOARD 31 GPIO 2 1 ACT LPO 1 0
PROG 1 AUT C4 AUTON BOARD 31 GPIO 0 1 ACT LPO 0 0

// Commande manuel D3 / H3 et C2 / G2
PROG 1 AUT C5 AUTON BOARD 0 GPIO 0 1 ACT LPO 5 1
PROG 1 AUT C6 AUTON BOARD 0 GPIO 2 1 ACT LPO 4 1
PROG 1 AUT C7 AUTON BOARD 0 GPIO 2 1 ACT LPO 5 0
PROG 1 AUT C8 AUTON BOARD 0 GPIO 0 1 ACT LPO 4 0

// Commande manuel B1 / CROSS
PROG 1 AUT C9 AUTON BOARD 31 GPIO 0 2 ACT LPO 2 1
PROG 1 AUT CA AUTON BOARD 31 GPIO 2 2 ACT LPO 3 1
PROG 1 AUT CB AUTON BOARD 31 GPIO 2 2 ACT LPO 2 0
PROG 1 AUT CC AUTON BOARD 31 GPIO 0 2 ACT LPO 3 0

// Commande automatique B1 / CROSS
PROG 1 AUT CD AUTON BOARD 31 GPIO 1 1 ACT LPO 2 1
PROG 1 AUT CE AUTON BOARD 31 GPIO 1 1 ACT LPO 3 0
PROG 1 AUT CF AUTON BOARD 31 GPIO 1 1 ACT LPO 2 1
PROG 1 AUT CG AUTON BOARD 31 GPIO 1 1 ACT LPO 3 0

// Commande automatique D3 / H3 et C2 / G2
PROG 1 AUT CH AUTON BOARD 31 GPIO 1 1 ACT LPO 4 1
PROG 1 AUT CI AUTON BOARD 31 GPIO 1 1 ACT LPO 5 0
PROG 1 AUT CJ AUTON BOARD 0 GPIO 1 1 ACT LPO 4 1
PROG 1 AUT CK AUTON BOARD 0 GPIO 1 1 ACT LPO 5 0

// Commande automatique CROSS
PROG 1 AUT CL AUTON BOARD 0 GPIO 1 1 ACT LPO 1 1
PROG 1 AUT CM AUTON BOARD 0 GPIO 1 1 ACT LPO 0 0

//================================================//
// GESTION DES AUTOMATISMES DE LA CARTE 2         //
//================================================//

// Connect Rail 0	Voie M
// Connect Rail 1	Voie non connectée
// Connect Rail 2	Voie non connectée
// Connect Rail 3	Voie non connectée

// LPO 0		Ouverture barrière passage à niveau 
// LPO 1		Fermeture barrière passage à niveau
// LPO 2		Led bouton orange
// LPO 3		Led mode manuel
// LPO 4		Led tramway devant grue
// LPO 5		Led tramway devant gare terminale 

// GPIO input 0		non connecté
// GPIO input 1		Bouton orange
// GPIO input 2		Détecton passage tramway devant capteur magnétique
// GPIO input 3		Contact mode manuel

// Track Status	0	Afficheur 7 segments DIO
// Track Status	1	Afficheur 7 segments CLK
// Track Status	2	Potentiomètre inertie	
// Track Status	3	Potentiomètre vitesse

//////////////////////////////////////////////////////////////////////////
// MODE MANUEL 
//////////////////////////////////////////////////////////////////////////

// Détection commutation en mode manuel et activation du mode Manuel uniquement sur track 0
PROG 2 AUT M0 AUTON BOARD 2 GPIO 3 0 ACT MANUAL0

// Détection commutation en mode manuel et éteint la LED orange
PROG 2 AUT M1 AUTON BOARD 2 GPIO 3 0 ACT LPO 2 0

// Allume led mode manuel
PROG 2 AUT M3 AUTON BOARD 2 GPIO 3 0 ACT LPO 3 1

// Remise à zéro du GPIO 3
PROG 2 AUT M4 AUTON BOARD 2 GPIO 3 0 ACT GPIO 3 0

// Eteint affichage des LEDs du TCO
PROG 2 AUT M5 AUTON BOARD 2 GPIO 3 0 ACT LPO 4 0
PROG 2 AUT M6 AUTON BOARD 2 GPIO 3 0 ACT LPO 5 0

// Réinitialise le compteur de GPIO
PROG 2 AUT M7 AUTON BOARD 2 GPIO 3 0 ACT GPIO 2 0


//////////////////////////////////////////////////////////////////////////
// MODE AUTOMATIQUE 
//////////////////////////////////////////////////////////////////////////

// Détection commutation en mode automatique et activation du mode automatique
PROG 2 AUT A1 AUTON BOARD 2 GPIO 3 1 ACT AUTOMATIC

// Détection commutation en mode automatique et Eteint led mode manuel
PROG 2 AUT A2 AUTON BOARD 2 GPIO 3 1 ACT LPO 3 0

// Détection commutation en mode automatique et stop le tramway
PROG 2 AUT A3 AUTON BOARD 2 GPIO 3 1 ACT TRACK 0 SPEED 0 FORW INERTIA 0

// Allume la LED orange du bouton sur appui bouton
PROG 2 AUT A4 AUTOFF BOARD 2 GPIO 1 1 ACT LPO 2 1

// Démarre le tramway pour le ramener sur la droite, le tramway doit être devant la gare de train
PROG 2 AUT A5 AUTOFF BOARD 2 GPIO 1 1 ACT TRACK 0 SPEED 5 BACK INERTIA 27

// Réinitialise GPIO 2 pour comptage position tramway
PROG 2 AUT A6 AUTOFF BOARD 2 GPIO 1 1 ACT GPIO 2 0

// Réinitialise GPIO 1 sur relachement bouton
PROG 2 AUT A7 AUTOFF BOARD 2 GPIO 1 0 ACT GPIO 1 0

//////////////////////////////////////////////////////////////////////////
// AFFICHAGE POSITION TRAMWAY SUR ARRET POUR TCO
//////////////////////////////////////////////////////////////////////////

// Allume led TCO droite M0
PROG 2 AUT P1 AUTOFF BOARD 2 GPIO 2 2 ACT LPO 5 1

// Stop le tramway 
PROG 2 AUT P2 AUTOFF BOARD 2 GPIO 2 2 ACT TRACK 0 SPEED 0 BACK INERTIA 27

// Arme timer 1 d'attente en gare
PROG 2 AUT P3 AUTOFF BOARD 2 GPIO 2 2 ACT TIMER 1 5

// Redémarre le tramway sur déclenchement timer 1
PROG 2 AUT P4 AUTOFF BOARD 2 TIMER 1 ACT TRACK 0 SPEED 5 FORW INERTIA 27

// Eteint led TCO droite M0
PROG 2 AUT P5 AUTOFF BOARD 2 GPIO 2 4 ACT LPO 5 0

// Gare intermédiaire
PROG 2 AUT P6 AUTOFF BOARD 2 GPIO 2 6 ACT TRACK 0 SPEED 0 FORW INERTIA 27

// Arme timer 1 d'attente en gare intermédiaire
PROG 2 AUT P7 AUTOFF BOARD 2 GPIO 2 6 ACT TIMER 1 5

// ferme les barrieres
PROG 2 AUT B1 AUTOFF BOARD 2 GPIO 2 8 ACT LPO 0 0 
PROG 2 AUT B2 AUTOFF BOARD 2 GPIO 2 8 ACT LPO 1 1 

PROG 2 AUT B3 AUTOFF BOARD 2 TIMER 2 ACT LPO 0 0 
PROG 2 AUT B4 AUTOFF BOARD 2 TIMER 2 ACT LPO 1 1 

// ouvre les barrieres
PROG 2 AUT B5 AUTOFF BOARD 2 GPIO 2 12 ACT LPO 0 1
PROG 2 AUT B6 AUTOFF BOARD 2 GPIO 2 12 ACT LPO 1 0

PROG 2 AUT B7 AUTOFF BOARD 2 GPIO 2 20 ACT LPO 0 1
PROG 2 AUT B8 AUTOFF BOARD 2 GPIO 2 20 ACT LPO 1 0

PROG 2 AUT B9 AUTOFF BOARD 2 GPIO 2 2 ACT LPO 0 1
PROG 2 AUT BA AUTOFF BOARD 2 GPIO 2 2 ACT LPO 1 0

// Stop le tramway 
PROG 2 AUT P8 AUTOFF BOARD 2 GPIO 2 14 ACT TRACK 0 SPEED 0 FORW INERTIA 27

// Allume led TCO gauche M0
PROG 2 AUT P9 AUTOFF BOARD 2 GPIO 2 14 ACT LPO 4 1

// Arme timer 2 d'attente en gare
PROG 2 AUT PA AUTOFF BOARD 2 GPIO 2 14 ACT TIMER 2 5

// Redémarre le tramway sur déclenchement timer 2
PROG 2 AUT PB AUTOFF BOARD 2 TIMER 2 ACT TRACK 0 SPEED 5 BACK INERTIA 27

// Eteint led TCO gauche M0
PROG 2 AUT PC AUTOFF BOARD 2 GPIO 2 16 ACT LPO 4 0

// Gare intermédiaire retour
PROG 2 AUT PD AUTOFF BOARD 2 GPIO 2 22 ACT TRACK 0 SPEED 0 FORW INERTIA 27

// Arme timer 3 d'attente en gare intermédiaire
PROG 2 AUT PE AUTOFF BOARD 2 GPIO 2 22 ACT TIMER 3 5

// Redémarre le tramway sur déclenchement timer 3
PROG 2 AUT PF AUTOFF BOARD 2 TIMER 3 ACT TRACK 0 SPEED 5 BACK INERTIA 27

// Réinitialise GPIO 2 pour nouveau cycle aller-retour
PROG 2 AUT PG AUTOFF BOARD 2 GPIO 2 24 ACT GPIO 2 0







